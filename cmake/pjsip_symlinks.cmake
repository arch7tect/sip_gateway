if(NOT DEFINED LIB_DIR)
    message(FATAL_ERROR "LIB_DIR must be set.")
endif()

set(_lib_names "")
if(DEFINED LIBS AND NOT "${LIBS}" STREQUAL "")
    set(_lib_names "${LIBS}")
    string(REPLACE "," ";" _lib_names "${_lib_names}")
endif()

if(_lib_names STREQUAL "")
    file(GLOB _suffix_libs "${LIB_DIR}/lib*-*.a")
    foreach(_lib_path IN LISTS _suffix_libs)
        get_filename_component(_file "${_lib_path}" NAME)
        string(REGEX REPLACE "-.*\\.a$" "" _base "${_file}")
        if(NOT _base STREQUAL "${_file}")
            list(APPEND _lib_names "${_base}")
        endif()
    endforeach()
    list(REMOVE_DUPLICATES _lib_names)
endif()

foreach(_lib_name IN LISTS _lib_names)
    set(_target "${LIB_DIR}/${_lib_name}.a")
    if(EXISTS "${_target}")
        continue()
    endif()
    file(GLOB _matches "${LIB_DIR}/${_lib_name}-*.a")
    list(SORT _matches)
    list(LENGTH _matches _match_count)
    if(_match_count EQUAL 0)
        continue()
    endif()
    list(GET _matches 0 _source)
    file(CREATE_LINK "${_source}" "${_target}" SYMBOLIC)
    if(NOT EXISTS "${_target}")
        file(COPY_FILE "${_source}" "${_target}")
    endif()
endforeach()
